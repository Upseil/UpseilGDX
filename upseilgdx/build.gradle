// UpseilGDX Parent

apply plugin: 'base'
apply plugin: 'maven-publish'

allprojects {
    apply plugin: "eclipse"

	group = 'com.upseil.gdx'
    version = '0.1.0-SNAPSHOT'
    
    ext {
        appName = 'UpseilGDX'
		gradleTarget = '3.2.1'
		javaTarget = '1.8'
        gdxTarget = '1.9.6'
        gwtTarget = '2.8.0'
        artemisTarget = '2.1.0'
        jacksonTarget = '2.8.8'
        jacksonGwtTarget = '0.14.2'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

subprojects {
    apply plugin: 'java'

	sourceCompatibility = "$javaTarget"
	[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
}
configure(subprojects.findAll {it.name != 'tools'}) {
	task sourceJar(type: Jar, dependsOn: classes) {
		from sourceSets.main.allSource
		classifier "sources"
	}

	publishing {
	    publications {
			"$project.name"(MavenPublication) {
				artifactId "$rootProject.name-$project.name"
			
				from components.java
				artifact sourceJar
			}
	    }
	}
}

tasks.eclipse.doLast {
    delete ".project"
}

task wrapper (type: Wrapper) {
	group = 'help'
	description = "Configures the wrapper for the target version $gradleTarget."
	
	gradleVersion = "$gradleTarget"
}