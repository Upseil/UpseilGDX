// UpseilGDX Parent

ext {
	upseilGdxPath = '.'
	
    appName = 'UpseilGDX'
	gradleTarget = '3.2.1'
	javaTarget = '1.8'
    gdxTarget = '1.9.6'
    gwtTarget = '2.8.0'
    artemisTarget = '2.1.0'
    jacksonTarget = '2.8.8'
    jacksonGwtTarget = '0.14.2'
}

apply from: "$upseilGdxPath/tools/upseilgdx-setup.gradle"
apply plugin: 'maven-publish'

allprojects {
	group = 'com.upseil.gdx'
    version = '0.1.0-SNAPSHOT'
}

configure(subprojects.findAll {it.name != 'tools'}) {
	task sourceJar(type: Jar, dependsOn: classes) {
		from sourceSets.main.allSource
		classifier "sources"
	}

	publishing {
	    publications {
			"$project.name"(MavenPublication) {
				artifactId "$rootProject.name-$project.name"
			
				from components.java
				artifact sourceJar
			}
	    }
	}
}

model {
	tasks.publishCorePublicationToMavenLocal {
		dependsOn 'publishUtilPublicationToMavenLocal'
	}
	tasks.publishArtemisPublicationToMavenLocal {
		dependsOn 'publishCorePublicationToMavenLocal'
	}
	tasks.publishGwtPublicationToMavenLocal {
		dependsOn 'publishCorePublicationToMavenLocal'
	}
}